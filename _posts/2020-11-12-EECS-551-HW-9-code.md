---
title: EECS 551 HW 9 code
date: 2020-11-12 19:30:11 +0800
categories: [Learning, UMICH, EECS551]
tags: [EECS551, UMICH, Data Science, Linear Algebra, DSP, ML]
seo:
  date_modified: 2020-12-02 10:51:18 +0800
---
```julia
using LinearAlgebra
"""
lr_schatten(Y, reg::Real)
Compute the regularized low−rank matrix approximation as the minimizer over `X`
of `1/2 \\|Y − X\\|^2 + reg R(x)`
where `R(X)` is the Schatten p−norm of `X` raised to the pth power, for `p=1/2`,
i.e., `R(X) = \\sum_k (\\sigma_k(X))^{1/2}`
In:
− `Y` `M by N` matrix
− `reg` regularization parameter
Out:
− `Xh` `M by N `solution to above minimization problem
"""
function lr_schatten(Y, reg::Real)
    U,s,V = svd(Y);
    r = length(s);
    l = 0;
    bound = 1.5*reg^(2/3);
    for i = 1:r
        if s[i] > bound
            s[i] = 4/3*s[i]*cos(1/3*acos(-(3^(3/2)*reg)/(4*s[i]^(3/2))))^2;
            l = i;
        else
            break;
        end
    end
    return U[:,1:l]*Diagonal(s[1:l])*V[:,1:l]'
end
```
```julia
using LinearAlgebra
"""
haveCommonRoot = common_root(p1, p2 ; atol)
Determine whether the polynomials described by input coefficient vectors
`p1` and `p2` share a common root, to within an absolute tolerance parameter `atol`.
Assume leading coefficients `p1[1]` and `p2[1]` are nonzero.
In:
− `p1` is a vector of length `m + 1` with `p1[1] != 0`
that defines an `m`th degree polynomial of the form:
`P1(x) = p1[1] x^m + p1[2] x^(m − 1) + ... + p1[m] x + p1[m + 1]`
− `p2` is a vector of length `n + 1` with `p2[1] != 0`
that defines an `n`th degree polynomial of the form:
`P2(x) = p2[1] x^n + p2[2] x^(n − 1) + ... + p2[n] x + p2[n + 1]`
Option:
− `atol` absolute tolerance for calling `isapprox`
Out:
− `haveCommonRoot` = `true` when `P1` and `P2` share a common root, else `false`
"""
function common_root(p1::AbstractVector, p2::AbstractVector ; atol::Real=1e-6)
    compan = c -> [-transpose(c); [I zeros(length(c)-1)]]
    m = length(p1) - 1;
    n = length(p2) - 1;
    A = compan(p1[2:end] / p1[1])
    B = compan(p2[2:end] / p2[1])
    return isapprox(det(kron(Matrix(I,n,n),A)+kron(-B,Matrix(I,m,m))),0,atol = atol);
end
```
```julia
using LinearAlgebra
"""
zmax, zmin = outlying_zeros(p, v0, nIters)
Use power iteration to compute the largest and smallest magnitude zeros,
respectively, of the polynomial defined by the input coefficients
In:
− `p` vector of length `n + 1` defining the polynomial
`P(x) = p[1] x^n + p[2] x^n−1 + ... + p[n] x + p[n + 1]` with `p[1] != 0`
Option:
− `v0` vector of length `n` with initial guess of an eigenvector; default randn
− `nIters` number of power iterations to perform; default 100
Out:
− `zmax` zero of `P(x)` with largest magnitude
− `zmin` zero of `P(x)` with smallest magnitude
"""
function outlying_zeros(p ; v0::AbstractVector{<:Real}=randn(length(p)-1), nIters::Int=100)
    n = length(p);
    p_ = reverse(p)
    compan = c -> [-transpose(c); [I zeros(length(c)-1)]];
    A = compan(p[2:end]/p[1])
    A_ = compan(p_[2:end]/p_[1])
    v0_ = copy(v0)
    for i = 1:nIters
        v1 = A*v0
        v2 = A_*v0_
        v1_norm = norm(v1)
        v2_norm = norm(v2)
        v0 = v1/v1_norm;
        v0_ = v2/v2_norm;
    end
    zmax = v0'*(A*v0);
    if isnan(1/(v0_'*(A_*v0_)))
        zmin = 0;
    else
        zmin = 1/(v0_'*(A_*v0_))
    end
    return zmax,zmin
end
```